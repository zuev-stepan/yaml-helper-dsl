grammar yaml.helper.dsl.YamlGenDsl with org.eclipse.xtext.common.Terminals

generate yamlGenDsl "http://www.helper.yaml/dsl/YamlGenDsl"

Source:
	(entries+=SourceElement)*
;

SourceElement:
	Field | Import
;
	
Import:
	'import' importURI=STRING
;

AnyField:
	Field | AnyNestedField
;

Field:
	(help+=HELP_STRING)*
	(root?='root')? name=ID ('extends' superField=[Field])? body=Body
;

Body:
	{Body} '{' (elements+=BodyElement)* '}'
;
	
BodyElement:
	Extend | AnyNestedField | Property
;
	
Extend:
	'extend' parentSubfieldName=STRING body=Body
;

AnyNestedField:
	NestedField | NestedFields
;
	
NestedField:
	(help+=HELP_STRING)*
	'field' (mandatory?='mandatory')? (default?='default')? ('extends' superField=[Field])? body=Body
;

NestedFields:
	(help+=HELP_STRING)*
	'fields' ('extend' superField=[Field])? body=Body
;

Property:
	StringProperty | ValueProperty | ValidatorProperty
;

StringProperty:
	key=('key' | 'name' | 'hint')':' value=STRING
;

ValueProperty:
	key=('default' | 'values')':' value=Value
;

ValidatorProperty:
	key='validator' ':' value=Validators ('(' help=STRING ')')?;

Validators:
	values+=SingleValidator ('|' values+=SingleValidator)*
;

SingleValidator:
	SimpleValidator | RegexValidator | ListValidator
;

SimpleValidator:
	value=('int' | 'float' | 'boolean' | 'string')
;

RegexValidator:
	value=STRING
;

ListValidator:
	value=('list' | 'list+') ('<' elementValidators=Validators '>')?
;

Value:
	ListValue | StringValue | LiteralValue
;

ListValue:
	'[' values+=Value (',' values+=Value)* ']'
|
	{ListValue} '[' ']'
;

StringValue:
	value=STRING
;

LiteralValue:
	IntValue | FloatValue | BooleanValue
;

IntValue:
	value=(HEX | MY_INT)
;

FloatValue:
	value=FLOAT
;

BooleanValue:
	TrueBooleanValue | FalseBooleanValue
;

TrueBooleanValue:
	value=('True' | 'true')
;

FalseBooleanValue:
	value=('False' | 'false')
;

terminal HEX: '0' ('x'|'X')(('0'..'9')|('a'..'f')|('A'..'F'))+;
terminal MY_INT: INT;
terminal EXT_INT: MY_INT ('e'|'E')('-'|'+') MY_INT;
FLOAT hidden(): MY_INT '.' (EXT_INT | MY_INT);
terminal HELP_STRING: '@' !('\n'|'\r')* ('\r'? '\n')?;
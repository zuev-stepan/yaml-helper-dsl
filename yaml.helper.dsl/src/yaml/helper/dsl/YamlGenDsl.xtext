grammar yaml.helper.dsl.YamlGenDsl with org.eclipse.xtext.common.Terminals

generate yamlGenDsl "http://www.helper.yaml/dsl/YamlGenDsl"

Source:
	(entries+=SourceElement)*
;

SourceElement:
	Field | Import
;
	
Import:
	'import' path=STRING
;

AnyField:
	Field | AnyNestedField
;

Field:
	(help+=HELP_STRING)*
	(root?='root')? name=ID ('extends' superField=[Field])? body=Body
;

Body:
	{Body} '{' (elements+=BodyElement)* '}'
;
	
BodyElement:
	Extend | AnyNestedField | Property
;
	
Extend:
	'extend' parentSubfieldName=STRING body=Body
;

AnyNestedField:
	NestedField | NestedFields
;
	
NestedField:
	(help+=HELP_STRING)*
	'field' (mandatory?='mandatory')? (default?='default')? ('extends' superField=[Field])? body=Body
;

NestedFields:
	(help+=HELP_STRING)*
	'fields' ('extend' superField=[Field])? body=Body
;

Property:
	StringProperty | ValuesProperty | TypeProperty
;

StringProperty:
	key=('key' | 'name' | 'hint')':' value=STRING
;

ValuesProperty:
	key=('default' | 'values')':' value=Values
;

TypeProperty:
	key='type' ':' value=('int' | 'float' | 'string' | 'list' | 'any' | STRING) ('(' help=STRING ')')?;

Values:
	'[' (values+=Values)+ ']'
|
	string=STRING
;

terminal HELP_STRING : '@' !('\n'|'\r')* ('\r'? '\n')?;